apply plugin: 'com.android.application'

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
def superpowered_sdk_path = properties.getProperty('superpowered.dir')

android {

    compileSdkVersion 25
    buildToolsVersion "24.0.3"

    defaultConfig {

        applicationId = "com.fesskiev.mediacenter"
        minSdkVersion 21
        targetSdkVersion 25
        versionCode = 1
        versionName = "1.0.0"

        jackOptions {
            enabled true
        }

        ndk {
            abiFilters 'armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64'
        }

        externalNativeBuild {
            cmake {
                arguments '-DANDROID_PLATFORM=android-16', '-DANDROID_TOOLCHAIN=clang', '-DANDROID_ARM_NEON=TRUE', '-DANDROID_STL=gnustl_static', "-DPATH_TO_SUPERPOWERED:STRING=${superpowered_sdk_path}"
                cFlags '-O3', '-fsigned-char'
                cppFlags '-fsigned-char', "-I${superpowered_sdk_path}"
            }
        }
    }

    signingConfigs {
        debug {
            storeFile file("debug.keystore")
        }
    }

    buildTypes {
        release {
            minifyEnabled = false

            proguardFiles.add(file('proguard-rules.txt'))
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    sourceSets {
        main {
            jniLibs.srcDirs = ['src/main/jni']
        }
    }

    externalNativeBuild {
        cmake {
            path 'src/main/jni/CMakeLists.txt'
        }
    }
}

ext {
    supportLibVersion = '25.0.1'
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile files('libs/jaudiotagger-2.2.4.jar')

    compile "com.android.support:appcompat-v7:${supportLibVersion}"
    compile "com.android.support:recyclerview-v7:${supportLibVersion}"
    compile "com.android.support:cardview-v7:${supportLibVersion}"
    compile "com.android.support:design:${supportLibVersion}"
    compile "com.android.support:support-v4:${supportLibVersion}"
    compile "com.android.support:percent:${supportLibVersion}"


    compile 'com.vk:androidsdk:1.6.5'
    compile 'com.github.clans:fab:1.6.3'
    compile 'com.github.bumptech.glide:glide:3.7.0'
    compile 'com.google.android.exoplayer:exoplayer:r1.5.10'
    compile 'io.reactivex:rxandroid:1.2.0'
    compile 'io.reactivex:rxjava:1.1.5'
    compile 'com.squareup.okhttp3:okhttp:3.4.1'
    compile 'com.squareup.retrofit2:retrofit:2.0.2'
    compile 'com.squareup.retrofit2:converter-gson:2.0.2'
    compile 'com.squareup.retrofit2:adapter-rxjava:2.1.0'
    compile 'com.squareup.sqlbrite:sqlbrite:0.7.0'
    compile 'org.greenrobot:eventbus:3.0.0'
}
